buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.0'
    }
}

configurations {
    embedJar
}

apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    compile project(':core-iface')
    embedJar project(':core-impl')
}

task copyLibraryJars(type: Copy, dependsOn: processResources) {
    from configurations.embedJar
    into "$buildDir/resources/main/jars"
    exclude { details -> details.file.name.startsWith('core-iface') }

    // "*.jar" resources in the shadowed JAR are unfortunately unzipped by the Gradle Shadow plugin
    // @see https://github.com/johnrengelman/shadow/issues/111
    rename { String fileName -> fileName.replace('.jar', '.jar.embed') }
}

classes {
    dependsOn copyLibraryJars
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name,
                   'Implementation-Version': project.version,
                   'Implementation-Vendor-Id': project.group,
                   'Specification-Title': project.name,
                   'Specification-Version': project.version,
                   'Main-Class': 'Main'
    }
}
